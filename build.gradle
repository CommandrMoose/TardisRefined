plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4+" apply false
}



architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
           // parchment("org.parchmentmc.data:parchment-${rootProject.mappings}")
        }
        implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    }
}

def getTimestamp() {
    def timestamp = java.time.Instant.now()
    def formatter = java.time.format.DateTimeFormatter.ofPattern("dd_MM_yy_HHmm")
    return "[" + timestamp.atZone(java.time.ZoneId.systemDefault()).format(formatter) + "]"
}


def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.jar_name
    version = "${rootProject.minecraft_version}-${rootProject.mod_version}-${getTimestamp()}-${gitBranch()}"
    group = rootProject.maven_group

    repositories {
        maven {
            url "https://cursemaven.com"
        }

        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }

        maven { url 'https://jitpack.io' }

        flatDir {
            dirs '../modlibs'
        }


    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
